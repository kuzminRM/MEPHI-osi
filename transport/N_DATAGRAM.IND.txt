;параметры:  address (число), userdata (буфер)
out CurrentSystemName() " n datagrams __start"
; проверяем размер полученного буфера
sizeof(userdata) < 3 if too_short_userdata_tag
out "length ok"

; проверяем контрольную сумму
userdata unbufferit crcFromInput 1 userDataNoCrc sizeof(userdata)-1
varcrc crcCalculated $userDataNoCrc
$crcFromInput != $crcCalculated if crc_compare_fail_tag
out "crc ok"

; распаковываем
userDataNoCrc unbufferit currentReqNumInput 1 entityNum 1 transportUserData sizeof(userDataNoCrc)-2
out "unpack ok"

; если к нам пришел запрос на connect, то насильно сбрасываем счетчики запросов
$entityNum != 1 if skip_set_zero_tocorrebt_req_num_tag
0 setto currentReqNumMy
$currentReqNumTheir == 1 if skip_set_zero_tocorrebt_req_num_tag
0 setto currentReqNumTheir
skip_set_zero_tocorrebt_req_num_tag:

; если пришло число больше чем мы ждем, то забываем про "пропущенный/ые" пакет/ы, считаем что он потерян
$currentReqNumTheir >= $currentReqNumInput if skip_input_more_then_their_tag
$currentReqNumInput setto currentReqNumTheir
skip_input_more_then_their_tag:

; если порядковый номер тот, что мы сейчас ждем, то начинаем ждать следующий номер
$currentReqNumTheir != $currentReqNumInput if number_compare_fail_tag
$currentReqNumTheir + 1 setto currentReqNumTheir

; определяем какой запрос пришел
$entityNum == 1 if t_con_req_tag
$entityNum == 3 if t_con_resp_tag
$entityNum == 5 if t_discon_req_tag
$entityNum == 7 if t_data_req_tag
out "some unknown entityNum = " $entityNum

return

we_are_dicronnected_tag:
out "we are disconnected"
return

too_short_userdata_tag:
out "too short user data" $userdata
return

crc_compare_fail_tag:
out "crc compare fail"
return

number_compare_fail_tag:
out "number compare fail wait for " $currentReqNumTheir " actual " $currentReqNumInput
return

t_con_req_tag:
out "received t con req"
0 setto isDisconnected
$address setto connectedAddr
address $address sendup T_CONNECT.IND
return

t_con_resp_tag:
out "received t con resp"
$address setto connectedAddr
address $address sendup T_CONNECT.CONF
return

t_discon_req_tag:
; проверяем что мы не находимся в состоянии isDisconnected
$isDisconnected == 1 if we_are_dicronnected_tag
out "received t discon req"
sendup T_DISCONNECT.IND
1 setto isDisconnected
0 setto currentReqNumMy
0 setto currentReqNumTheir
return

t_data_req_tag:
out "received t data req"
userdata $transportUserData sendup T_DATA.IND
return